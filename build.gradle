plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'jacoco'
	id 'org.sonarqube' version '4.0.0.2929'
}

group = 'me.mocadev'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	runtimeOnly 'com.mysql:mysql-connector-j'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	testImplementation "org.mockito:mockito-core:3.+"

	testImplementation "org.testcontainers:testcontainers:1.19.0"

	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	testImplementation "com.redis.testcontainers:testcontainers-redis-junit:1.6.4"

	implementation(platform("software.amazon.awssdk:bom:2.20.136"))
	implementation("commons-io:commons-io:2.13.0")
	implementation('software.amazon.awssdk:aws-core:2.20.69')
	implementation('software.amazon.awssdk:sdk-core:2.20.66')
	implementation('software.amazon.awssdk:sts:2.20.69')
	implementation('software.amazon.awssdk:s3:2.20.68')
	testImplementation("org.testcontainers:localstack:1.19.0")

	testImplementation 'com.tngtech.archunit:archunit:1.1.0'
}

jacoco {
	toolVersion = "0.8.8"
}

tasks.named('test') {
	useJUnitPlatform()

	jacoco {}
	finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
		csv.required = false

		xml.destination(file("build/jacoco/jacoco.xml"))
		html.destination(file("build/jacoco/jacoco.html"))
	}

	finalizedBy(tasks.jacocoTestCoverageVerification)
}

tasks.jacocoTestCoverageVerification {
	violationRules {
		rule {
			enabled = true
			element = "CLASS"

			limit {
				counter = "LINE"
				value = "COVEREDRATIO"
				minimum = BigDecimal.valueOf(0.5)
			}

			limit {
				counter = "LINE"
				value = "TOTALCOUNT"
				maximum = BigDecimal.valueOf(300)
			}

			excludes = List.of(
				"*.test.*",
				"*.controller.*",
				"com.jyujyu.dayonetest.MyCalculatorApplication",
				"com.jyujyu.dayonetest.DayonetestApplication",
			)
		}
	}
}

sonar {
	properties {
		property("sonar.host.url", "http://localhost:9000")
		property("sonar.login", "token_name")
		property("sonar.sources", "src/main/java")
		property("sonar.tests", "src/test/java")
		property("sonar.sourceEncoding", "UTF-8")
		property("sonar.projectKey", "project_key")
		property("sonar.projectName", "project_name")
		property("sonar.coverage.jacoco.xmlReportPaths", "build/jacoco/jacoco.xml")
	}
}
